grammar iit.dsl.transspecs.TransSpecs with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
    
generate transSpecs "http://www.dsl.iit/TransSpecs"

KinematicsQuery:
    Title name= ID
    framesList = FramesList
    fks += FK*
    iks += IK*
;

Title : "Robot" | "Model" ;

FramesList:
    "Frames""{"
        items += Frame
        (','items += Frame)*
     "}"
;
Frame: name=ID;

FK:
    "FK" name=ID "{"
        poses += Pose*
        velocities += Velocity*
        jacobians += Jacobian*
    "}"
;

IK: IKpos | IKvel;

IKpos:
    "IKpos" name=ID "{"
        specs=IKspecs
    "}"
;

IKvel:
    "IKvel" name=ID "{"
        specs=IKspecs
    "}"
;

IKspecs: {IKspecs}
    "vect"   ":" vkind=VectorKind
    "frames" ":" frames = FramePair
;

enum VectorKind :
    lin='linear' | ang='angular' | full='pose'
;

FK_quantity: Pose | Velocity | Jacobian;

Pose:
    "pose" ":" frames = FramePair
;
Velocity:
    "velocity" ":" frames = FramePair
;
Jacobian:
    ("Jacobian"|"jacobian") ":" frames = FramePair
;

FramePair:
    target=[Frame] "wrt" reference=[Frame]
;
